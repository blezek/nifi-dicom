
plugins {
     id 'io.github.lhotari.gradle-nar-plugin' version '0.5.1'
     id "com.github.breadmoirai.github-release" version "2.2.12"
    id 'java'
}

ext {
    nifiVersion = '1.27.0'
}

group 'com.blezek.nifi.dicom'
version project.nifiVersion

// Adds the NAR taget for NIFI
nar {
    manifest {
        attributes(
                'Nar-Group': project.group,
                'Nar-Id': project.name,
                'Nar-Version': project.version
        )
    }
}


test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    // DCM4CHE has their own Maven...
    maven { url "https://www.dcm4che.org/maven2/" }
    maven { url "https://jcenter.bintray.com/" }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation(
            "org.apache.nifi:nifi-mock:" + project.nifiVersion,
            'org.hamcrest:hamcrest-library:1.3',
    )

    implementation group: 'javax.json', name: 'javax.json-api', version: '1.0'
    implementation group: 'com.google.guava', name: 'guava', version: '23.0'

    // DCM4CHE
    implementation group: 'org.dcm4che', name: 'dcm4che-core', version: '3.3.8'
    implementation group: 'org.dcm4che', name: 'dcm4che-image', version: '3.3.8'
    implementation group: 'org.dcm4che', name: 'dcm4che-net', version: '3.3.8'
    implementation group: 'org.apache.nifi', name: 'nifi-api', version: project.nifiVersion
    implementation group: 'org.apache.nifi', name: 'nifi-utils', version: project.nifiVersion

    // These are extra PixelMed libraries that hopefully could be removed
    // https://mvnrepository.com/artifact/javax.vecmath/vecmath
    implementation group: 'javax.vecmath', name: 'vecmath', version: '1.5.2'
    // https://mvnrepository.com/artifact/org.hsqldb/hsqldb
    implementation group: 'org.hsqldb', name: 'hsqldb', version: '2.4.0'
    // https://mvnrepository.com/artifact/commons-net/commons-net
    implementation group: 'commons-net', name: 'commons-net', version: '3.6'
    // https://mvnrepository.com/artifact/javax.jmdns/jmdns
    implementation group: 'javax.jmdns', name: 'jmdns', version: '3.4.1'

    // Database and misc
    implementation 'org.apache.derby:derby:10.14.1.0'
    implementation group: 'org.apache.derby', name: 'derbynet', version: '10.14.1.0'
    implementation 'org.jdbi:jdbi3-core:3.1.0'
    implementation "com.h2database:h2:1.3.170"
    implementation "com.google.guava:guava:23.0"
    implementation "org.flywaydb:flyway-core:5.0.7"
    implementation 'com.google.code.gson:gson:2.8.2'

    // Encryption
    implementation 'org.bouncycastle:bcprov-jdk15on:1.52'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.52'

    // CSV
    implementation "com.opencsv:opencsv:4.0"

    // See https://stackoverflow.com/a/43574427/334619
    // Need to add back in activation framework that is deprecated from
    // JDK 9 and removed in 11
    implementation "javax.xml.bind:jaxb-api:2.2.11"
    implementation "com.sun.xml.bind:jaxb-core:2.2.11"
    implementation "com.sun.xml.bind:jaxb-impl:2.2.11"
    implementation "javax.activation:activation:1.1.1"
}

githubRelease {
    token System.getenv('GITHUB_NIFI_TOKEN') ?: "0"
    owner "blezek"
    repo "nifi-dicom"

    allowUploadToExisting true
    releaseAssets = files("build/libs/nifi-dicom-${project.version}.nar")

    body = {
        """\
# Info

This release updates to NiFi version 1.27.0

## ChangeLog

* ${changelog().call().replace('\n', '\n* ')}
"""
    }
}


